defmodule TwelveDays do
  @ordinals %{
    2 => "second", 3 => "third", 4 => "fourth",
    5 => "fifth", 6 => "sixth", 7 => "seventh", 8 => "eighth",
    9 => "ninth", 10 => "tenth", 11 => "eleventh", 12 => "twelfth"
  }

  @gifts %{
     2 => "two Turtle Doves",
     3 => "three French Hens",
     4 => "four Calling Birds",
     5 => "five Gold Rings",
     6 => "six Geese-a-Laying",
     7 => "seven Swans-a-Swimming",
     8 => "eight Maids-a-Milking",
     9 => "nine Ladies Dancing",
    10 => "ten Lords-a-Leaping",
    11 => "eleven Pipers Piping",
    12 => "twelve Drummers Drumming"
  }

  @doc """
  Given a `number`, return the song's verse for that specific day, including
  all gifts for previous days in the same line.
  """
  @spec verse(number :: integer) :: String.t()
  def verse(1), do: 
    "On the first day of Christmas my true love gave to me: a Partridge in a Pear Tree."
  def verse(number) do
    gifts_to_partridge = number..2//-1 |> Enum.map(& @gifts[&1]) |> Enum.join(", ")
    "On the #{@ordinals[number]} day of Christmas my true love gave to me: " 
    <> gifts_to_partridge
    <> ", and a Partridge in a Pear Tree."
  end

  @doc """
  Given a `starting_verse` and an `ending_verse`, return the verses for each
  included day, one per line.
  """
  @spec verses(starting_verse :: integer, ending_verse :: integer) :: String.t()
  def verses(starting_verse, ending_verse), do:
    starting_verse..ending_verse |> Enum.map(&verse/1) |> Enum.join("\n")

  @doc """
  Sing all 12 verses, in order, one verse per line.
  """
  @spec sing() :: String.t()
  def sing, do: verses(1, 12)
end
